#!/usr/bin/env zsh

# fzf
[[ -f ~/.fzf.zsh ]] && source ~/.fzf.zsh

# Configure the system.
unset GOARCH
unset GOROOT
unset GOPATH
export EDITOR="vim"
export LANG="en_US.UTF-8"
export LC_ALL=$LANG
export HISTSIZE=10000000
export SAVEHIST=$HISTSIZE
setopt EXTENDED_HISTORY

export ORIGIN_PATH=$PATH

# NOTE: `*-magic` is known buggy in some zsh versions.
# (see: https://github.com/robbyrussell/oh-my-zsh/blob/e604eaf55/lib/misc.zsh#L3)
#
# The following error message is shown in my Ubuntu environment
# whenever I paste something into my terminal, so I disabled it.
# """
# bracketed-paste-magic:zle:38: not enough arguments for -U
# """
export DISABLE_MAGIC_FUNCTIONS="true"

# Oh My ZSH!
export ZSH=$HOME/.oh-my-zsh
plugins=(git z)
if [[ -d "$ZSH" ]]; then
  for plugin_path in $ZSH/custom/plugins/*; do
    plugin="$(basename $plugin_path)"
    if [[ -f "$plugin_path/$plugin.plugin.zsh" ]] &&
      [[ "$plugin" != "zsh-syntax-highlighting" ]]; then
      plugins+=("$plugin")
    fi
  done
  # `zsh-syntax-highlighting` must be last at plugins
  plugins+=("zsh-syntax-highlighting")

  ZSH_THEME="vin"
  source $ZSH/oh-my-zsh.sh
fi

# Make the completion scripts for bash compatible with zsh
# https://stackoverflow.com/questions/3249432/can-a-bash-tab-completion-script-be-used-in-zsh
autoload bashcompinit && bashcompinit
# Activate rc files
mkdir -p "$HOME/.rc"

# sorted rc files
for p in $(find "$HOME/.rc/" -not -type d | sort); do
  source "$p"
done

# Prevent terminal emulator from executing Tmux automatically
# e.g.) TERMINAL_EMULATOR=JetBrains-JediTerm (on IntelliJ)
if [[ -z "$TMUX" && -z "$TERMINAL_EMULATOR" ]]; then
  session="default"
  (tmux has-session -t "$session" && tmux attach -t "$session") || tmux new -s "$session"
else
  # Prevent PATH from being duplicated in Tmux session
  # ref: https://stackoverflow.com/questions/13058578/how-to-prevent-tmux-from-filling-up-the-global-path-variable-with-duplicated-pat
  export PATH=$ORIGIN_PATH
fi
